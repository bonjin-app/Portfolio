plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
    id 'war'
}

group = 'kr.co.bonjin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // =============================== Spring Boot Starter ===============================
    // WEB - MVC, Tomcat, Jackson
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // VIEW - Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    // JPA - H2, Derby, Hsqldb
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // =============================== External library ===============================
    // Maria DB
    implementation "org.mariadb.jdbc:mariadb-java-client"

    implementation 'com.querydsl:querydsl-jpa'

    // =============================== Support library ===============================
    // LomBok DI
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}


// QueryDSL Config
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}